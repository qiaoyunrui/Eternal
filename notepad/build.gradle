if (notepadEnable.toBoolean()) {
    // 作为 library 来依赖
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {

    signingConfigs {
        Release {
            keyAlias internalGetSigningConfigs().keyAlias
            keyPassword internalGetSigningConfigs().keyPassword
            storeFile file(storeFilePath)
            storePassword internalGetSigningConfigs().storePassword
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        if (!notepadEnable.toBoolean()) {
            applicationId rootProject.ext.mainApplicationId + ".notepad"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.globalVersionCode as int
        versionName rootProject.ext.globalVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.Release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (notepadEnable.toBoolean()) {
                manifest.srcFile 'src/main/library/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/application/AndroidManifest.xml'
            }
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    implementation project(':eternal')
}
